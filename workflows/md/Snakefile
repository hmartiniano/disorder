# vi: syntax=python
# vi: ft=python

import os

os.environ["OMP_NUM_THREADS"] = "32"

configfile: "config.yml"

rule all:
  input:
    "prd.0003"

##singularity: "docker://gromacs/gromacs"


rule convert_pdb:
  input: "protein.pdb"
  output: gro="protein.gro", top="topol.top"
  #singularity: "docker://gromacs/gromacs"
  shell:
    "echo '5\n3' | gmx pdb2gmx -f {input} -ignh -o {output.gro}"


rule setup_box:
  input: "protein.gro"
  output: "newbox.gro"
  #singularity: "docker://gromacs/gromacs"
  shell:
    "echo Protein | gmx editconf -f {input} -o {output} -princ -d 2"


rule solvate:
  input: gro="newbox.gro"
  output: gro="solv.gro"
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx solvate -cp newbox.gro -cs tip4p.gro -o solv.gro -p topol.top"


rule setup_add_ions:
  input: "solv.gro"
  output: "ions.tpr"
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx grompp -f ions.mdp -c solv.gro -p topol.top -o ions.tpr -maxwarn 1"


rule add_ions:
  input: "ions.tpr"
  output: "solv_ions.gro"
  #singularity: "docker://gromacs/gromacs"
  shell: "echo SOL | gmx genion -s ions.tpr -o solv_ions.gro -p topol.top -pname NA -nname CL -neutral -conc 0.1"

#wget http://www.mdtutorials.com/gmx/umbrella/Files/minim.mdp
#wget http://www.mdtutorials.com/gmx/umbrella/Files/npt.mdp
#
rule setup_minimize:
  input: "solv_ions.gro"
  output: "em.tpr"
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx grompp -f minim.mdp -c solv_ions.gro -p topol.top -o em.tpr -maxwarn 10"


rule minimize:
  input: "em.tpr"
  output: protected("em.gro"), protected("em.log"), protected("em.edr")
  #singularity: "docker://gromacs/gromacs"
  threads: 8
  shell: "gmx mdrun -v -deffnm $(basename {input} .tpr) -ntmpi 1 -ntomp {threads}"


rule setup_equilibrium1:
  input: mdp="eq1.mdp", gro="em.gro"
  output: "eq1.tpr"
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx grompp -f {input.mdp} -c {input.gro} -p topol.top -r {input.gro} -o {output} -maxwarn 10"


rule equilibrium1:
  input: "eq1.tpr"
  output: gro=protected("eq1.gro"),
          cpt=protected("eq1.cpt"),
          xtc=protected("eq1.xtc"),
          log=protected("eq1.log")
  threads: 8
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx mdrun -deffnm $(basename {input} .tpr) -ntmpi 1 -ntomp {threads}"

rule setup_equilibrium2:
  input: mdp="eq2.mdp", gro="eq1.gro", cpt="eq1.cpt"
  output: "eq2.tpr"
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx grompp -f {input.mdp} -c {input.gro} -t {input.cpt} -o {output} -maxwarn 10"
  #shell: "gmx grompp -f {input.mdp} -c {input.gro} -p topol.top -r {input.gro} -o {output} -maxwarn 10"
  

rule equilibrium2:
  input: tpr="eq2.tpr"
  output: gro=protected("eq2.gro"),
          cpt=protected("eq2.cpt"),
          xtc=protected("eq2.xtc"),
          log=protected("eq2.log")
  threads: 8
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx mdrun -deffnm $(basename {input.tpr} .tpr) -ntmpi 1 -ntomp {threads}"


rule setup_production:
  input: mdp="prd.mdp", cpt="eq2.cpt", gro="eq2.gro", tpr="eq2.tpr"
  output: "prd.tpr"
  #singularity: "docker://gromacs/gromacs"
  shell:
    "gmx grompp -f {input.mdp} -c {input.gro} -t {input.cpt} -o {output} -maxwarn 10"


rule production:
  input: tpr="prd.tpr"
  output: gro="prd.gro", cpt="prd.cpt", edr="prd.edr", log="prd.log", xtc="prd.xtc", chk="prd.0001"
  threads: 8
  #singularity: "docker://gromacs/gromacs"
  shell: "gmx mdrun -deffnm prd -ntmpi 1 -ntomp {threads} && touch prd.0001"


def extend_simulation_inputs(wildcards):
    prev = int(wildcards.block) - 1
    return {"chk": f"prd.{prev:04d}"}


rule extend_simulation:
  input: unpack(extend_simulation_inputs)  #gro="prd.gro", cpt="prd.cpt", edr="prd.edr", log="prd.log", xtc="prd.xtc"
  output: touch("prd.{block}")
  wildcard_constraints: block="[0-9][0-9][0-9][2-9]|[0-9][0-9][1-9][0-9]|[0-9][1-9][0-9][0-9]|[1-9][0-9][0-9][0-9]"
  threads: 8
  params: nsteps = lambda w: int(w.block) * 50000000, prev=lambda w: "{:04d}".format(int(w.block) - 1)
  #singularity: "docker://gromacs/gromacs"
  shell: "cp prd.tpr prd.tpr.{params.prev} && cp prd.cpt prd.cpt.{params.prev} && gmx convert-tpr -s prd.tpr -nsteps {params.nsteps} -o prd.tpr && gmx mdrun -deffnm prd -cpi prd.cpt -ntmpi 1 -ntomp {threads} -noappend "
